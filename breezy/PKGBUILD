# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=breezy
pkgver=3.2.2
pkgrel=3
pkgdesc='A decentralized revision control system with support for Bazaar and Git file formats'
arch=('i686' 'x86_64')
url='https://www.breezy-vcs.org/'
license=('GPL2')
groups=('VCS')
depends=('python-configobj'
         'python-fastimport'
         'python-dulwich'
         'python-patiencediff'
         'python-fastbencode')
makedepends=(
  'python-setuptools'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-devel'
  'gcc'
  'cython'
)
provides=(bzr)
conflicts=(bzr)
replaces=(bzr)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/breezy-team/breezy/archive/brz-${pkgver}.tar.gz
        '0002-add-msys2-certs-location.patch')
sha256sums=('5661f2e2348a60db4af9c4c8f417a3da3f0bbc02f7a41ce988baba5e1100f3f1'
            '8f3a1c151c9ceb8b2ace12dc1c80bd123810e2e77a2c784385d5ad039f0bd3bb')

prepare(){
  cd "${srcdir}/${pkgname}-brz-${pkgver}"

  patch -p1 -i ${srcdir}/0002-add-msys2-certs-location.patch
}

build() {
  cd "${srcdir}/${pkgname}-brz-${pkgver}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${pkgname}-brz-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl
  cp "${pkgdir}"/usr/bin/brz "${pkgdir}"/usr/bin/bzr
}
