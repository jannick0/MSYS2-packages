# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_realname='colormake'
pkgname="${_realname}-git"
pkgver=r8.9c1d2e6
pkgrel=2
pkgdesc='Colorized build output'
url='https://github.com/renatosilva/colormake'
license=(BSD)
arch=(any)

provides=(${_realname})
conflicts=(${_realname})
depends=(make)
makedepends=(git)
install="${_realname}.install"
_commit="9c1d2e608c1ce165e7df40f4a9e4f1c0145875a5"
source=("git+https://github.com/renatosilva/colormake#commit=${_commit}"
        "${_realname}.install")
sha256sums=('SKIP'
            '77404b4ebad97738edabb6c8a1c1958cb73e28dedeed5a85a4e0ceaf1469fd73')
msys2_references=(
  'archlinux'
)

pkgver() {
    cd "${srcdir}/${_realname}"
    printf "r%s.%s" $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

package() {
    cd "${srcdir}/${_realname}"
    install -Dm755 colormake.sh  "${pkgdir}/usr/bin/colormake"
    install -Dm644 LICENSE       "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
}
