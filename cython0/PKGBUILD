# Maintainer: Alexandre Ferreira < alex.jorge.m [at] gmail.com >

_realname=cython
pkgbase='cython0'
pkgname=('cython0')
pkgver=0.29.37
pkgrel=1
pkgdesc='C-Extensions for Python'
arch=('i686' 'x86_64')
url='https://cython.org/'
license=('spdx:Apache-2.0')
depends=('python')
conflicts=('cython')
makedepends=(
  'gcc'
  'python-devel'
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/cython/cython/archive/${pkgver}.tar.gz")
sha256sums=('824eb14045d85c5af677536134199dd6709db8fb0835452fd2d54bc3c8df8887')

prepare() {
  cd "${_realname}-${pkgver}"
}

build() {
  cd "${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
}
