# Maintainer: David Mott <mott.david.j@gmail.com>

pkgname=dialog
pkgver=1.3_20230209
pkgrel=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="https://invisible-island.net/dialog/"
license=('spdx:LGPL-2.1-or-later')
makedepends=('ncurses-devel' 'autotools' 'gcc' 'gettext-devel')
depends=('ncurses' 'libintl')
source=(https://invisible-mirror.net/archives/$pkgname/$pkgname-${pkgver/_/-}.tgz)
sha256sums=('0c26282305264be2217f335f3798f48b1dce3cf12c5a076bf231cadf77a6d6a8')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --build=${CYGWIN_CHOST} \
    --host=${CYGWIN_CHOST} \
    --prefix=/usr \
    --enable-nls \
    --with-ncursesw
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install-full
}
