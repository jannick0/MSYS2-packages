# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=doxygen
pkgver=1.9.8
pkgrel=2
pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
arch=('i686' 'x86_64')
url="http://www.doxygen.org/"
license=('GPL')
depends=("gcc-libs" "libsqlite" "libiconv")
makedepends=("cmake" "ninja" "flex" "python" "libsqlite-devel" "libiconv-devel" 'gcc')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/doxygen/doxygen/archive/Release_${pkgver//./_}.tar.gz)
sha256sums=('77371e8a58d22d5e03c52729844d1043e9cbf8d0005ec5112ffa4c8f509ddde8')

build() {
  local extra_config=Release
  if check_option "debug" "y"; then
    extra_config=Debug
  fi

  mkdir -p build-${CHOST} && cd build-${CHOST}

  /usr/bin/cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=${extra_config} \
    -Dbuild_wizard=OFF \
    -Dbuild_search=OFF \
    -Duse_sqlite3=ON \
    -Duse_libclang=OFF \
    ../${pkgname}-Release_${pkgver//./_}

  /usr/bin/cmake --build .
}

package() {
  cd ${srcdir}/build-${CHOST}
  DESTDIR="${pkgdir}" /usr/bin/cmake --install .
}
