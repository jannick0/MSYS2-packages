# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=elinks
pkgver=0.16.1.1
pkgrel=2
epoch=
pkgdesc="Full-Featured Text WWW Browser (net-utils)"
arch=("i686" "x86_64")
url="https://github.com/rkd77/elinks"
license=('GPL')
groups=('net-utils')
depends=('libbz2'
         'libexpat'
         'libgcrypt'
         'libgnutls'
         'libiconv'
         'libidn'
         'liblzma'
         'libtre'
         'libzstd'
         'libintl'
         'perl'
         'python3'
         'zlib')
makedepends=('autotools'
             'gettext-devel'
             'libbz2-devel'
             'libexpat-devel'
             'libgcrypt-devel'
             'libgnutls-devel'
             'libiconv-devel'
             'libidn-devel'
             'liblzma-devel'
             'libtre-devel'
             'libzstd-devel'
             'perl-devel'
             'python-devel'
             'zlib-devel'
             'xmlto'
             'groff'
             'doxygen'
             'gcc')
provides=("elinks-git")
conflicts=("elinks-git")
replaces=("elinks-git")
source=("elinks-${pkgver}.tgz::https://github.com/rkd77/elinks/archive/v${pkgver}.tar.gz"
        "https://github.com/rkd77/elinks/commit/5b1b478a07a977e82836d82486a5a0681b90d87f.patch"
        "https://github.com/rkd77/elinks/commit/51273efada535811d65985df0743152e9660d338.patch")
sha256sums=('d07e55ede144d532212b2b5b92fb81df4360ddd2b156ccaf09cfa4aa218e1aed'
            '5054e56929248b84d8f5d40ff6bf6a597d24570b90b72ea373a0c6aac1ae9ae0'
            '55c424e5f2794dc29268de962227d9175fcd17d38abee6d95002ed5f35c5a863')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np1 -i ../5b1b478a07a977e82836d82486a5a0681b90d87f.patch
  patch -Np1 -i ../51273efada535811d65985df0743152e9660d338.patch

  ./autogen.sh
}

build() {
  cd "${pkgname}-${pkgver}"

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  export MSYSTEM=CYGWIN
  export PYTHON=/usr/bin/python3

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --build=${CYGWIN_CHOST} \
    --without-openssl \
    --with-lzma \
    --without-guile \
    --with-perl \
    --with-python \
    --without-lua

  make
}

check() {
  cd "${pkgname}-${pkgver}"

  make -k check
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir/" install
  rm -f $pkgdir/usr/share/locale/locale.alias
  rm -f $pkgdir/usr/lib/charset.alias
}
