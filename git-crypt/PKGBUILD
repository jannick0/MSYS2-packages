# Maintainer: Ladislav Laska <krakonos@krakonos.org>

pkgname="git-crypt"
pkgver=0.7.0
pkgrel=4
pkgdesc="Transparent file encryption in git"
arch=("i686" "x86_64")
url="https://www.agwa.name/projects/git-crypt/"
license=("spdx:GPL-3.0-or-later")
source=("https://www.agwa.name/projects/git-crypt/downloads/${pkgname}-${pkgver}.tar.gz"
        "openssl-3.0.patch")
sha256sums=('50f100816a636a682404703b6c23a459e4d30248b2886a5cf571b0d52527c7d8'
            'b8dc43540bfd80993359890a6064ef51a932d9e677b3cd62527c6cff4bdfc1f4')
makedepends=("openssl-devel" "make" "gcc")
depends=("gnupg" "git" "zlib" "openssl" "gcc-libs")

prepare() {
  cd "$pkgname-$pkgver"

  # from https://github.com/archlinux/svntogit-community/tree/c2618a7eb44a46636a78bdb85c6eabda956543cd/trunk
  patch -Np1 -i "${srcdir}"/openssl-3.0.patch
}

build () {
  cd "$pkgname-$pkgver"

  CPPFLAGS+=" -D_GNU_SOURCE"
  make
}
package () {
  cd "$pkgname-$pkgver"
  make PREFIX="/usr" DESTDIR="${pkgdir}" install
}
