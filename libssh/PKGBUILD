pkgbase=libssh
pkgname=('libssh' 'libssh-devel')
pkgver=0.10.6
pkgrel=1
pkgdesc='Library for accessing ssh client services through C libraries'
url='https://www.libssh.org/'
license=('spdx:LGPL-2.1+')
arch=('i686' 'x86_64')
depends=('zlib' 'libopenssl')
makedepends=('cmake' 'python' 'openssl-devel' 'gcc' 'zlib-devel')
source=("https://www.libssh.org/files/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('1861d498f5b6f1741b6abc73e608478491edcf9c9d4b6630eef6e74596de9dc1'
            'SKIP')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>

prepare() {
  cd $pkgname-$pkgver
}

build() {
  mkdir -p build && cd build

  # to make GLOB_TILDE available
  CFLAGS+=" -D_GNU_SOURCE"

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_GSSAPI=OFF \
    -DUNIT_TESTING=OFF
  make
  make DESTDIR="$srcdir/dest" install
}

package_libssh() {
  mkdir -p $pkgdir/usr/bin
  cp -rf $srcdir/dest/usr/bin/* $pkgdir/usr/bin/
}

package_libssh-devel() {
  mkdir -p $pkgdir/usr/include
  cp -rf $srcdir/dest/usr/include/* $pkgdir/usr/include/

  mkdir -p $pkgdir/usr/lib
  cp -rf $srcdir/dest/usr/lib/* $pkgdir/usr/lib/
}
