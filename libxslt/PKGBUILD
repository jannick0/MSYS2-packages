# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=libxslt
pkgname=('libxslt' 'libxslt-devel')
pkgver=1.1.39
pkgrel=1
pkgdesc="XML stylesheet transformation library"
arch=('i686' 'x86_64')
url="https://gitlab.gnome.org/GNOME/libxslt/-/wikis/"
license=('custom')
makedepends=('gcc' 'libxml2-devel' 'libgcrypt-devel' 'autotools')
checkdepends=('docbook-xml')
source=("https://download.gnome.org/sources/libxslt/${pkgver%.*}/${pkgbase}-${pkgver}.tar.xz")
sha256sums=('2a20ad621148339b0759c4d4e96719362dee64c9a096dbba625ba053846349f0')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  export lt_cv_deplibs_check_method='pass_all'
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --build=${CYGWIN_CHOST} \
    --host=${CYGWIN_CHOST} \
    --target=${CYGWIN_CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-shared \
    --enable-static \
    --with-crypto \
    --with-debugger \
    --with-python=no

  make
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make check
}

package_libxslt() {
  depends=('libxml2' 'libgcrypt')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/share
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  rm -f ${pkgdir}/usr/bin/*-config
  cp -rf ${srcdir}/dest/usr/share/man ${pkgdir}/usr/share/
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgbase}/COPYING"
}

package_libxslt-devel() {
  pkgdesc="Libxslt headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libxslt=${pkgver}" 'libxml2-devel' 'libgcrypt-devel')

  mkdir -p ${pkgdir}/usr/{bin,share}
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share/aclocal ${pkgdir}/usr/share/
  cp -rf ${srcdir}/dest/usr/share/doc ${pkgdir}/usr/share/
  rm -rf ${pkgdir}/usr/share/doc/libxslt-python-${pkgver}
}
