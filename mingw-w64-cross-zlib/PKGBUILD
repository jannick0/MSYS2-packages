# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=zlib
_mingw_suff=mingw-w64-cross
pkgname=("${_mingw_suff}-${_realname}")
pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP"
pkgver=1.3
pkgrel=4
arch=('i686' 'x86_64')
url="https://mingw-w64.sourceforge.io/"
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=()
makedepends=("${_mingw_suff}-gcc" 'autotools')
options=('!strip' '!buildflags')
source=("https://zlib.net/fossils/${_realname}-${pkgver}.tar.gz"
        01-zlib-1.2.11-1-buildsys.mingw.patch
        03-dont-put-sodir-into-L.mingw.patch
        04-fix-largefile-support.patch)
sha256sums=('ff0ba4c292013dbc27530b3a81e1f9a813cd39de01ca5e0f8bf355702efa593e'
            '8175e70c980ffc48a2e90c6f0431ddb80ef6ff690f9e8906dd6a756238552fa5'
            '7287d12db57dcf0f66964c861100bf06ebe1ddcb243e7ee0773fcab1b2935596'
            '4d8ebda55b95d19cd7c467c486d4c42d2bad6db9ff5dca361f6d8c24120e5f30')
msys2_references=(
  'archlinux: zlib'
)

_targets="x86_64-w64-mingw32ucrt i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  grep -A 24 '^  Copyright' zlib.h > LICENSE

  patch -p1 -i ${srcdir}/01-zlib-1.2.11-1-buildsys.mingw.patch
  patch -p1 -i ${srcdir}/03-dont-put-sodir-into-L.mingw.patch
  patch -p1 -i ${srcdir}/04-fix-largefile-support.patch
}

build() {
  for _target in ${_targets}; do
    msg "Configuring ${_realname} for ${_target}"
    cp -rf ${srcdir}/${_realname}-${pkgver} ${srcdir}/${_realname}-${_target}
    cd ${srcdir}/${_realname}-${_target}
    unset CC CXX
    CHOST=${_target} ./configure --prefix=/opt/${_target} --shared
    make
  done
}

package() {
  for _target in ${_targets}; do
    msg "Installing ${_realname} for ${_target}"
    cd ${srcdir}/${_realname}-${_target}
    make DESTDIR=${pkgdir} install
  done
}
