# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=mpdecimal
pkgname=('mpdecimal' 'mpdecimal-devel')
pkgver=2.5.1
pkgrel=2
pkgdesc="Package for correctly-rounded arbitrary precision decimal floating point arithmetic"
arch=('i686' 'x86_64')
url="https://www.bytereef.org/mpdecimal/index.html"
license=('spdx:BSD-2-Clause')
depends=('gcc-libs')
makedepends=('autotools' 'gcc')
checkdepends=('wget')
source=("http://www.bytereef.org/software/${pkgname}/releases/${pkgname}-${pkgver}.tar.gz"
        "mpdecimal-2.5.0-msys-mingw.patch")
sha256sums=('9f9cd4c041f99b5c49ffb7b59d9f12d95b683d88585608aa56a6307667b2b21f'
            'a1ac23df732d1fabefd68cd553ddaa559b11abf87134feb72e1c7d4890a8ca59')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Nbp1 -i ${srcdir}/mpdecimal-2.5.0-msys-mingw.patch
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package_mpdecimal() {
  depends=('gcc-libs')
  cd ${srcdir}/dest

  mkdir -p ${pkgdir}/usr/bin
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}

package_mpdecimal-devel() {
  depends=("mpdecimal=${pkgver}")
  pkgdesc="mpdecimal headers and libraries"

  cd ${srcdir}/dest
  cp -rf usr ${pkgdir}/usr
  rm -r ${pkgdir}/usr/bin
}
