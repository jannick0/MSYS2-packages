# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Class-Method-Modifiers
pkgname=perl-${_realname}
pkgver=2.14
pkgrel=1
pkgdesc="provides Moose-like method modifiers"
arch=('any')
url="https://metacpan.org/dist/Class-Method-Modifiers"
groups=('perl-modules')
depends=('perl')
makedepends=('perl-Test-Fatal' 'perl-Test-Requires' 'perl-Test-Needs')
license=('GPL' 'PerlArtistic')
source=("https://cpan.metacpan.org/authors/id/E/ET/ETHER/Class-Method-Modifiers-${pkgver}.tar.gz")
sha256sums=('3704ee6e75434f2a48e565a9c418e1593d611105ac6d658227549c6ba260cd1d')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 \
  PERL_AUTOINSTALL=--skipdeps \
  PERL5LIB="" \
  MODULEBUILDRC=/dev/null \
  PERL_MM_OPT="INSTALLDIRS=vendor" \
  PERL_MB_OPT="--installdirs vendor" \
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
