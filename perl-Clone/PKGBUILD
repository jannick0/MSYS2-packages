# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Clone
pkgname=perl-${_realname}
pkgver=0.46
pkgrel=2
pkgdesc='Recursive copy of nested objects.'
arch=('i686' 'x86_64')
url='https://github.com/garu/Clone'
groups=('perl-modules')
license=('GPL' 'PerlArtistic')
depends=('perl')
makedepends=('perl-devel' 'gcc' 'make')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/G/GA/GARU/${_realname}-${pkgver}.tar.gz")
sha256sums=('aadeed5e4c8bd6bbdf68c0dd0066cb513e16ab9e5b4382dc4a0aafd55890697b')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd ${_realname}-${pkgver}
    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
