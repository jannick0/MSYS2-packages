# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=Data-OptList
pkgname=perl-${_realname}
pkgver=0.113
pkgrel=1
pkgdesc="Parse and validate simple name/value option pairs"
arch=('any')
url="https://metacpan.org/release/Data-OptList"
license=('PerlArtistic' 'GPL')
depends=('perl-Params-Util' 'perl-Scalar-List-Utils' 'perl-Sub-Install')
makedepends=('perl-ExtUtils-MakeMaker')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Data-OptList-${pkgver}.tar.gz)
sha512sums=('8c3a27218cf1a1fc9515be63586ea782c299cd193ab5003885a051a28d0d9c1e8d7ce6e4ddaffcdb37f1d2fa28363b4248a45f964b2429cea712253549ae2da2')

prepare() {
  cd ${_realname}-${pkgver}
}

build() {
  cd ${_realname}-${pkgver}
  PERL_MM_USE_DEFAULT=1 \
  PERL_AUTOINSTALL=--skipdeps \
  PERL5LIB="" \
  MODULEBUILDRC=/dev/null \
  PERL_MM_OPT="INSTALLDIRS=vendor" \
  PERL_MB_OPT="--installdirs vendor" \
  perl Makefile.PL
  make
}

check() {
  cd ${_realname}-${pkgver}
  PERL_MM_USE_DEFAULT=1 \
  PERL5LIB="" \
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"
}
