# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ExtUtils-Depends
pkgname=perl-${_realname}
pkgver=0.8001
pkgrel=2
pkgdesc="The Perl depends module"
arch=('any')
url="https://metacpan.org/dist/ExtUtils-Depends"
groups=('perl-modules')
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/X/XA/XAOC/${_realname}-${pkgver}.tar.gz"
        'ExtUtils-Depends-0.304.patch')
sha256sums=('673c4387e7896c1a216099c1fbb3faaa7763d7f5f95a1a56a60a2a2906c131c5'
            '4a476b3a0e05484e4807cdca13e654fa69b6128f257807d893bb3a39c924a9e6')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/ExtUtils-Depends-0.304.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
