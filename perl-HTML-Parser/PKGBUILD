# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTML-Parser
pkgname=perl-${_realname}
pkgver=3.81
pkgrel=2
pkgdesc="Perl HTML parser class"
arch=('i686' 'x86_64')
license=('spdx:Artistic-1.0-Perl' 'spdx:GPL-1.0-or-later')
url="https://metacpan.org/dist/HTML-Parser"
groups=('perl-modules')
depends=('perl-HTML-Tagset' 'perl-HTTP-Message' 'perl-URI' 'perl')
makedepends=('perl-devel' 'make' 'gcc')
checkdepends=('perl-Test-Pod')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz)
sha256sums=('c0910a5c8f92f8817edd06ccfd224ba1c2ebe8c10f551f032587a1fc83d62ff2')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"
}
