# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=Test-Deep
pkgname=perl-${_realname}
pkgver=1.204
pkgrel=1
pkgdesc="Extremely flexible deep comparison"
arch=('any')
url="https://metacpan.org/release/Test-Deep"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-Test-Simple' 'perl-Test-NoWarnings')
options=('!emptydirs')
groups=('perl-modules')
source=("https://cpan.metacpan.org/authors/id/R/RJ/RJBS/${_realname}-${pkgver}.tar.gz")
sha256sums=('b6591f6ccdd853c7efc9ff3c5756370403211cffe46047f082b1cd1611a84e5f')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL installdirs=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
