# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=calver
pkgname=python-calver
pkgver=2022.6.26
pkgrel=1
pkgdesc="Setuptools extension for CalVer package versions"
arch=('any')
url="https://github.com/di/calver"
license=('spdx:Apache-2.0')
depends=(
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('e05493a3b17517ef1748fbe610da11f10485faa7c416b9d33fd4a52d74894f8b')

build() {
  cd "${_pyname}-${pkgver}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
