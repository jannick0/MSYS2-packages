# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=certifi
pkgname="python-${_realname}"
pkgver=2023.11.17
pkgrel=1
pkgdesc="Python package for providing Mozilla's CA Bundle"
url='https://pypi.python.org/pypi/certifi'
license=('spdx:MPL-2.0')
arch=('any')
depends=("python")
makedepends=(
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=("https://pypi.io/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('9b469f3a900bf28dc19b8cfbf8019bf47f7fdd1a65a1d4ffb98fc14166beb4d1')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
