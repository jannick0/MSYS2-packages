# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=iniconfig
pkgname=python-iniconfig
pkgver=2.0.0
pkgrel=2
pkgdesc="brain-dead simple config-ini parsing"
url="https://github.com/pytest-dev/iniconfig"
license=('spdx:MIT')
arch=('any')
depends=('python')
makedepends=(
  'python-hatchling'
  'python-hatch-vcs'
  'python-build'
  'python-installer'
)
source=("$_pyname-$pkgver.tar.gz::https://github.com/RonnyPfannschmidt/iniconfig/archive/v$pkgver.tar.gz")
sha256sums=('ee33adf27310b0fc9b41abc504ca95925083762bcd7bec28cdbaf42fb215bcb3')

build() {
  cd "$srcdir/$_pyname-$pkgver"

  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "$_pyname-$pkgver"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

