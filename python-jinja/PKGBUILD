# Maintainer: Alexandre Ferreira < alex.jorge.m [at] gmail.com >

_realname=jinja
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=3.1.2
pkgrel=3
pkgdesc='A simple pythonic template language written in Python'
arch=('i686' 'x86_64')
license=('spdx:BSD-3-Clause')
url='http://jinja.pocoo.org/'
depends=('python-markupsafe')
makedepends=(
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/pallets/jinja/archive/${pkgver}.tar.gz")
sha256sums=('ecae76cd1a064d40eb46c5375f07953d747f4d65b68cd3fa5f02c91714b799fc')

prepare() {
  cd "${_realname}-${pkgver}"
}

build() {
  cd "${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.rst "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE.rst"
}
