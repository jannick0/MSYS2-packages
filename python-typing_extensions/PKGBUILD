# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=typing_extensions
pkgname=python-typing_extensions
pkgver=4.8.0
pkgrel=1
pkgdesc='Backported and Experimental Type Hints for Python 3.8+'
arch=(any)
url=https://github.com/python/typing_extensions
msys2_references=(
  'pypi: typing-extensions'
)
license=('spdx:PSF-2.0')
depends=(python)
makedepends=(
  python-build
  python-flit-core
  python-installer
)
source=("https://pypi.python.org/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('df8e4339e9cb77357558cbdbceca33c303714cf861d1eef15e1070055ae8b7ef')

build() {
  cd "${_pyname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_pyname}-${pkgver}"

  python -m unittest discover src
}

package() {
  cd "${_pyname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
}
