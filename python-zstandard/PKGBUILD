# Maintainer: Jeremy Drake <github@jdrake.com>

pkgname=python-zstandard
pkgver=0.22.0
pkgrel=1
pkgdesc='Python bindings to the Zstandard (zstd) compression library'
arch=('i686' 'x86_64')
url="https://github.com/indygreg/python-zstandard"
license=('spdx:BSD-3-Clause')
depends=("python")
makedepends=(
  "python-devel"
  'gcc'
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
options=('!emptydirs')
source=("https://github.com/indygreg/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('34ce7ef020afca1344c538a778e2a2e30dc43b11509aa4cb5cf076228d959ca7')

prepare() {
  cd "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${pkgname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
