# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=swig
pkgver=4.1.1
pkgrel=2
pkgdesc="Generate scripting interfaces to C/C++ code"
arch=('i686' 'x86_64')
url="https://www.swig.org/"
license=('custom')
depends=('zlib' 'libpcre2_8')
makedepends=('zlib-devel' 'pcre2-devel' 'autotools' 'gcc')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        0001-no-stdcall.patch)
sha256sums=('2af08aced8fcd65cdb5cc62426768914bedc735b1c250325203716f78e39ac9b'
            '61f4b47694e89f566d50702def201bafba2ac84c2b35b3f0a00a5ccb834acf62')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/0001-no-stdcall.patch

  ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}

  export MSYSTEM=CYGWIN
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure -build=${CYGWIN_CHOST} --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -D -m644 LICENSE-UNIVERSITIES "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE-UNIVERSITIES
}
