#Maintainer: Erwin Waterlander <waterlan@xs4all.nl>

pkgname=wcd
pkgver=6.0.5
pkgrel=1
pkgdesc='Wherever Change Directory'
arch=('i686' 'x86_64')
url='https://waterlan.home.xs4all.nl/'
license=('GPL')
depends=('libintl' 'libunistring' 'ncurses')
groups=('sys-utils')
makedepends=('perl' 'gettext-devel' 'libunistring-devel' 'ncurses-devel' 'make' 'gcc')
source=("https://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f2970e01a52886d330604490fdbedd7373faff0910ff2e14889435677f563763')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} install-profile
  cd ${srcdir}/${pkgname}-${pkgver}/doc
  install -D -m644 copying.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
