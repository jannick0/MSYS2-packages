# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=xorgproto
pkgver=2023.2
pkgrel=1
pkgdesc="combined X.Org X11 Protocol headers"
arch=(any)
license=('custom')
groups=('development')
conflicts=('xproto')
provides=('xproto')
replaces=('xproto')
url="https://xorg.freedesktop.org/"
makedepends=('meson' 'ninja' 'gcc')
source=("https://xorg.freedesktop.org/archive/individual/proto/${pkgname}-${pkgver}.tar.xz")
sha256sums=('b61fbc7db82b14ce2dc705ab590efc32b9ad800037113d1973811781d5118c2c')

prepare() {
  cd "${pkgname}-${pkgver}"

}

build() {
  mkdir "${srcdir}"/build && cd "${srcdir}"/build

  meson setup \
    --wrap-mode=nodownload \
    --auto-features=enabled \
    --buildtype=plain \
    --prefix=/usr \
    "../${pkgname}-${pkgver}"

  meson compile
}

package() {
  cd "${srcdir}"/build

  DESTDIR="$pkgdir" meson install
}
