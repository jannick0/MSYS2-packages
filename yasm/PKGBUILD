# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('yasm' 'yasm-devel')
pkgver=1.3.0
pkgrel=3
pkgdesc="A rewrite of NASM to allow for multiple syntax supported (NASM, TASM, GAS, etc.)"
arch=('i686' 'x86_64')
url="https://www.tortall.net/projects/yasm/"
license=('BSD' 'GPL2' 'LGPL2.1' 'PerlArtistic')
options=('staticlibs')
makedepends=('autotools' 'gcc' 'xmlto')
source=(https://www.tortall.net/projects/yasm/releases/${pkgname}-${pkgver}.tar.gz
        'yasm-1.2.0-1.src.patch'
        '001-debian-autoconf.patch')
sha256sums=('3dce6601b495f5b3d45b59f7d2492a340ee7e84b5beca17e48f862502bd5603f'
            '10a8dee681519d6fe14bf7f4dc9cb5b0f55cc7210c4e532e6939c4363cb0d3c0'
            '258af057cbf8fc055192552dc1b510f072ddb30c495502c00374c884347ec476')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p2 -i ${srcdir}/yasm-1.2.0-1.src.patch

  # https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=997338
  patch -p1 -i ${srcdir}/001-debian-autoconf.patch

  autoreconf -fi
}

build() {
  cd ${pkgname}-${pkgver}
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --prefix=/usr \
    --build="${CYGWIN_CHOST}"
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package_yasm() {
  mkdir -p ${pkgdir}/usr

  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_yasm-devel() {
  mkdir -p ${pkgdir}/usr

  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
